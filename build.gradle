apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.lxgaming"
archivesBaseName = "BungeePlayer"
version = "1.1.3"

configurations {
    provided {
        compile.extendsFrom(provided)
    }

    build.dependsOn("signJar")
    build.dependsOn("uploadArchives")
}

repositories {
    jcenter()
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    provided(fileTree(dir: "./libs/", include: ["*.jar"]))
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from {
        (configurations.compile - configurations.provided).findAll({
            it.isDirectory() || it.name.endsWith(".jar")
        }).collect({
            it.isDirectory() ? it : zipTree(it)
        })
    }
}

processResources {
    from("LICENSE")
    rename("LICENSE", "LICENSE-" + archivesBaseName)

    from (sourceSets.main.resources.srcDirs) {
        include("bungee.yml")
        expand("name": archivesBaseName, "version": version)
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        if (project.hasProperty("maven.url")) {
            mavenDeployer {
                project.logger.lifecycle("Uploading Archives")
                repository(url: project.property("maven.url"))

                pom {
                    groupId = project.group
                    artifactId = project.archivesBaseName
                    version = project.version
                    project {
                        name = project.archivesBaseName
                        packaging = "jar"

                        scm {
                            url = "https://github.com/LXGaming/BungeePlayer"
                            connection = "scm:git:https://github.com/LXGaming/BungeePlayer.git"
                            developerConnection = "scm:git:git@github.com:LXGaming/BungeePlayer.git"
                        }

                        issueManagement {
                            system = "GitHub Issues"
                            url = "https://github.com/LXGaming/BungeePlayer/issues"
                        }

                        licenses {
                            license {
                                name = "Apache License 2.0"
                                url = "https://opensource.org/licenses/Apache-2.0"
                                distribution = "repo"
                            }
                        }
                    }
                }
            }
        }
    }
}

task signJar {
    doFirst {
        if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
            project.logger.warn("========== [WARNING] ==========")
            project.logger.warn("")
            project.logger.warn("   This build is not signed!   ")
            project.logger.warn("")
            project.logger.warn("========== [WARNING] ==========")
            throw new StopExecutionException()
        }
    }

    doLast {
        jar.outputs.files.each {
            ant.signjar(
                jar: it,
                alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keyStorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified: project.property("signing.preserveLastModified"),
                tsaurl: project.property("signing.timestampAuthority"),
                digestalg: project.property("signing.digestAlgorithm")
            )
            project.logger.lifecycle("JAR Signed: " + it.name)

            signing.sign(it)
            project.logger.lifecycle("PGP Signed: " + it.name)
        }
    }
}